plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def android_builder_version = "4.0.1"

ext {
    android_builder_main_version = Integer.parseInt(android_builder_version.split("\\.")[0])
    android_builder_mid_version = Integer.parseInt(android_builder_version.split("\\.")[1])
}

android {
    this.rootProject.buildscript.configurations.classpath
            .resolvedConfiguration
            .firstLevelModuleDependencies.
            each {
                def name = it.name
                if (name.contains('com.android.tools.build:gradle')) {
                    def moduleVersion = it.moduleVersion
                    if (moduleVersion.contains("-")) {
                        def alphaversionArray = moduleVersion.split("-")[0]
                        def versionArray = alphaversionArray.toString().split("\\.")
                        ext.android_builder_main_version = Integer.parseInt(versionArray[0])
                        ext.android_builder_mid_version = Integer.parseInt(versionArray[1])
                    } else {
                        version = moduleVersion
                        android_builder_version = moduleVersion
                        ext.android_builder_main_version = Integer.parseInt(android_builder_version.split("\\.")[0])
                        ext.android_builder_mid_version = Integer.parseInt(android_builder_version.split("\\.")[1])
                    }
                }
            }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId = "com.yasinkacmaz.jetflix"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        if (android_builder_main_version >= 7 || (android_builder_main_version > 4 && android_builder_mid_version > 2)) {
            compose true
        }
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

//    testOptions {
//        unitTests.apply {
//            isIncludeAndroidResources = true
//            isReturnDefaultValues = true
//        }
//        animationsDisabled = true
//    }

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

kapt {
    correctErrorTypes = true
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_common_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_common_version"

    // AndroidX
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"
    implementation "androidx.core:core-ktx:1.6.0-alpha03"

    // Compose
    if (android_builder_main_version < 7) {
        add("kotlinCompilerPluginClasspath", "androidx.compose.compiler:compiler:$compose_version")
    }
    implementation "androidx.activity:activity-compose:$compose_activity_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.paging:paging-compose:$paging_compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:$compose_constraintlayout_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$compose_lifecycle_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-coil:$compose_accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$compose_accompanist_version"
    implementation "com.google.accompanist:accompanist-insets-ui:$compose_accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$compose_accompanist_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:$retrofit_kotlinx_serialization_converter_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"

    // Test
    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation "io.strikt:strikt-core:0.30.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3"

    androidTestImplementation "androidx.compose.ui:ui-test:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
